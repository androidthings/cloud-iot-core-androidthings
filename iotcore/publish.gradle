apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def VERSIONID = "1.0.0"
def GROUPID = "com.google.android.things"
def ARTIFACTID = "cloud-iot-core"

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/iotcore-release.aar")
            artifact(sourcesJar)
            artifact(javadocJar)

            groupId GROUPID
            artifactId ARTIFACTID
            version VERSIONID

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.name != "unspecified") {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['Production']

    pkg {
        repo = 'androidthings'
        name = ARTIFACTID
        userOrg = 'google'
        licenses = ['Apache-2.0']
        desc = 'Android Things client library for Google Cloud IoT Core.'
        publicDownloadNumbers = false
        version {
          name = VERSIONID
          gpg {
            sign = true
          }
        }
    }
}

// Source jar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.source
    classifier = 'sources'
}

// Javadoc jar
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.source
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += (project.files(android.getBootClasspath().join(File.pathSeparator))
            + files(ext.androidJar))
    options.links("https://developer.android.com/reference/");
    failOnError false
}
afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect
            { variant ->
                variant.javaCompile.classpath.files
            })
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}
